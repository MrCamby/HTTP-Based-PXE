#!ipxe

chain --autofree boot.cfg

# Detemine if using serial if yes skip set clear sequence
iseq ${cls} serial && goto ignore_cls ||
set cls:hex 1b:5b:4a  # ANSI clear screen sequence - "^[[J"
set cls ${cls:string}
:ignore_cls

# Detect Architecture
isset ${arch} && goto skip_arch_detect ||
cpuid --ext 29 && set arch x86_64 || set arch i386
:skip_arch_detect
isset ${menu} && goto ${menu} ||

isset ${ip} || dhcp || echo DHCP failed

:main_menu
clear menu
set space:hex 20:20
set space ${space:string}
iseq ${arch} x86_64 && set arch_a amd64 || set arch_a ${arch}
menu ${site_name}

item --gap Default:
item local ${space} Boot from local hdd

item --gap Distributions:
iseq ${menu_esxi} 1 && item esxi ${space} VMWare ESXi
iseq ${menu_freedos} 1 && item freedos ${space} FreeDOS
iseq ${menu_linux} 1 && item linux ${space} Linux Installs
iseq ${menu_windows} 1 && item windows ${space} Windows

item --gap Tools:
iseq ${menu_utils} 1 && item utils-${platform} ${space} Utilities ||
iseq ${arch} x86_64 && set bits 64 || set bits 32
item changebits ${space} Architecture: ${arch} (${bits}bit)
item shell ${space} iPXE shell
item netinfo ${space} Network card info
item lspci ${space} PCI Device List
item about ${space} About

isset ${menu} && set timeout 0 || set timeout ${boot_timeout}
choose --timeout ${timeout} --default ${menu} menu || goto local
echo ${cls}
goto ${menu} ||
goto change_menu

:change_menu
chain ${menu}.ipxe || goto error
goto main_menu

:error
echo Error occured, press any key to return to menu ...
prompt
goto main_menu

:local
echo Booting from local disks ...
exit 0

:shell
echo Type "exit" to return to menu.
set menu main_menu
shell
goto main_menu

:changebits
iseq ${arch} x86_64 && set arch i386 || set arch x86_64
goto main_menu


:about
chain about.ipxe 
goto main_menu

